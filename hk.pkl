// Download from https://github.com/jdx/hk/releases/download/v1.36.0/hk@1.36.0.zip to reduce reliance on GitHub
amends "hk@1.36.0/Config.pkl"
import "hk@1.36.0/Builtins.pkl"

// --- Python ---
local python_linters = new Mapping<String, Step> {
  // Fast Python linter (flake8/pylint/isort replacement)
  ["ruff"] = Builtins.ruff
  // Fast Python formatter (black replacement)
  ["ruff-format"] = Builtins.ruff_format
  // Validates Python files parse without syntax errors
  ["python-check-ast"] = (Builtins.python_check_ast) {
    exclude =
      List(
        "**/.venv/**",
        "**/__pycache__/**",
        "packages/api-service/**",
      )
  }
  // Detects leftover breakpoint()/pdb statements
  ["python-debug-statements"] = Builtins.python_debug_statements
}

local javascript_linters = new Mapping<String, Step> {
  // Fast JavaScript linter (eslint replacement)
  ["biome"] = (Builtins.biome) {
    glob = List("*.js", "*.ts") // Override file patterns
    fix = "biome check --write --unsafe --no-errors-on-unmatched {{ files }}"
  }
}

local openapi_linters = new Mapping<String, Step> {
  ["vacuum"] = Builtins.vacuum
}

// --- Config files ---
local config_linters = new Mapping<String, Step> {
  // Formats mise.toml configuration files
  ["mise"] = Builtins.mise
  // Validates TOML files for correctness
  ["taplo"] = Builtins.taplo
  // Formats TOML files consistently
  ["taplo-format"] = Builtins.taplo_format
  // Formats YAML files consistently
  ["yamlfmt"] = Builtins.yamlfmt
  ["pkl"] = Builtins.pkl
  ["pkl-format"] = Builtins.pkl_format
}

// --- GitHub Actions ---
local gh_actions_linters = new Mapping<String, Step> {
  ["pinact"] = Builtins.pinact
  // Audits GitHub Actions workflows for security vulnerabilities
  ["zizmor"] = Builtins.zizmor
}

// --- Containers ---
local containers_linters = new Mapping<String, Step> {
  ["hadolint"] = Builtins.hadolint
}

local all_linters = new Mapping<String, Step> {
  ...python_linters
  ...config_linters
  ...gh_actions_linters
  ...containers_linters
  ...javascript_linters
  ...openapi_linters
}

fail_fast = false

hooks {
  ["fix"] {
    fix = true
    steps = all_linters
  }
  ["check"] {
    steps = all_linters
  }
  ["pre-commit"] {
    fix = true
    stash = "git"
    steps = all_linters
  }
}
